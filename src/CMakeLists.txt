# 
# Note: This builds the project as an executable linked to a library built from
# its sources. This would be stupid and roundabout if it wasn't for how it also
# lets us share code between application and unit tests with no extra effort.
# 

set(CMAKE_INCLUDE_CURRENT_DIR ON)
set(CMAKE_AUTOMOC ON)

if(CMAKE_COMPILER_IS_GNUCXX)
	set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -std=gnu++0x")
endif()

# These three are managed by the version bump script!
set(VERSION_MAJOR 0)
set(VERSION_MINOR 9)
set(VERSION_PATCH 2)

configure_file(version.h.in version.h)



#
# --> Find libraries and stuff
#
find_package(Qt5Core REQUIRED)
find_package(Qt5Widgets REQUIRED)
find_package(Qt5WebKitWidgets REQUIRED)
find_package(Qt5Network REQUIRED)

if(WIN32)
	find_package(Qt5WinExtras)
endif(WIN32)



#
# --> Find sources
#
file(GLOB_RECURSE KCTViewer_FORMS ../forms/*.ui)
file(GLOB_RECURSE KCTViewer_SOURCES *.cpp *.h)
file(GLOB_RECURSE KCTViewer_RESOURCES ../resources/*.qrc)
file(GLOB KCTViewer_MAIN_CPP main.cpp)
file(GLOB KCTViewer_VERSION_H version.h)
list(REMOVE_ITEM KCTViewer_SOURCES ${KCTViewer_MAIN_CPP} ${KCTViewer_VERSION_H})

# Remove moc and uic files from the source list
file(GLOB KCTViewer_MOCS moc_*.cpp *_automoc.cpp ui_*.h)
list(REMOVE_ITEM KCTViewer_SOURCES "" ${KCTViewer_MOCS})

qt5_wrap_ui(KCTViewer_UIHEADERS ${KCTViewer_FORMS})
qt5_add_resources(KCTViewer_RESOURCE_SOURCES ${KCTViewer_RESOURCES})

set(KCTViewer_EXTRA_FILES "../resources/index.html")
set_source_files_properties(${KCTViewer_EXTRA_FILES} PROPERTIES HEADER_FILE_ONLY TRUE)




#
# --> Build the common library
#
add_library(KVBase ${KCTViewer_SOURCES} ${KCTViewer_UIHEADERS})
target_link_libraries(KVBase Qt5::Core Qt5::Widgets Qt5::WebKitWidgets Qt5::Network)

if(WIN32)
	target_link_libraries(KVBase Qt5::WinExtras wsock32 ws2_32)
endif(WIN32)



#
# --> Build the main application
#
add_executable(KCTViewer WIN32 ${KCTViewer_MAIN_CPP} ${KCTViewer_RESOURCE_SOURCES} ${KCTViewer_EXTRA_FILES} KCTViewer.rc)
target_link_libraries(KCTViewer KVBase)



#
# --> Installation
#
install(TARGETS KCTViewer RUNTIME DESTINATION bin)



#
# --> IDE Organization
#
source_group("Autogenerated" REGULAR_EXPRESSION "((qrc|ui)_(.*)|.*_automoc\\.cpp)")
